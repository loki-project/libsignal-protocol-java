apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 6
archivesBaseName = "signal-protocol-java"
version          = version_number
group            = group_info

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    test {
        java {
            srcDirs = ['src/test/java/', project(':tests').file('src/test/java')]
        }
    }
}

dependencies {
    implementation "org.whispersystems:curve25519-java:${curve25519_version}"
    implementation 'com.google.protobuf:protobuf-java:2.5.0'
    testImplementation ('junit:junit:3.8.2')
}

test {
    testLogging {
        events 'passed'
        showStandardStreams = true
    }
    include 'org/whispersystems/**'
}

signing {
    required { hasProperty("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "") {
            authentication(userName: "", password: "")
        }

        pom.project {
            name 'signal-protocol-java'
            packaging 'jar'
            description 'Signal Protocol cryptography library for Java'
            url 'https://github.com/loki-project/loki-messenger-android-protocol'

            scm {
                url 'scm:git@github.com:loki-project/loki-messenger-android-protocol.git'
                connection 'scm:git@github.com:loki-project/loki-messenger-android-protocol.git'
                developerConnection 'scm:git@github.com:loki-project/loki-messenger-android-protocol.git'
            }

            licenses {
                license {
                    name 'GPLv3'
                    url 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    name 'Niels Andriesse'
                }
            }
        }
    }
}

task installArchives(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository url: "file://${System.properties['user.home']}/.m2/repository"
        }
    }
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives(packageJavadoc) {
        type = 'javadoc'
    }
    archives packageSources
}

