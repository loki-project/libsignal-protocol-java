buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = "signal-protocol-android"
version          = version_number
group            = group_info

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    sourceSets {
        androidTest {
            java.srcDirs = ['src/androidTest/java', project(':tests').file('src/test/java')]
        }
    }
    defaultConfig {
        minSdkVersion 9
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${version}.aar"
                output.outputFileName = fileName
            }
        }
    }
}

repositories {
    google()
    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "org.whispersystems:curve25519-android:${curve25519_version}"
    implementation (project(':java')) {
        exclude group: 'org.whispersystems', module: 'curve25519-java'
    }
}

signing {
    required { hasProperty("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "") {
            authentication(userName: "", password: "")
        }

        pom.project {
            name 'signal-protocol-android'
            packaging 'aar'
            description 'Signal Protocol cryptography library for Android'
            url 'https://github.com/loki-project/loki-messenger-android-protocol'

            scm {
                url 'scm:git@github.com:loki-project/loki-messenger-android-protocol.git'
                connection 'scm:git@github.com:loki-project/loki-messenger-android-protocol.git'
                developerConnection 'scm:git@github.com:loki-project/loki-messenger-android-protocol.git'
            }

            licenses {
                license {
                    name 'GPLv3'
                    url 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    name 'Niels Andriesse'
                }
            }
        }
    }
}

task installArchives(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository url: "file://${System.properties['user.home']}/.m2/repository"
        }
    }
}

